#!/usr/bin/env nbb
;; Resolve `clojure -Spath` -- the SAME git/sha deps.edn dependencies
;; `clojure -M:test` uses -- into shadow-cljs.edn's :source-paths, so the
;; ClojureScript job always tests against the exact pinned versions and never
;; a hand-duplicated, driftable path list.
;;
;; nbb, not bb (ADR-2607173000: bb is retired as a script host here).
(require '[kotoba.lang.text :as str])
(def fs (js/require "node:fs"))
(def cp (js/require "node:child_process"))
(defn sh [& args]
  (let [r (.spawnSync cp (first args) (to-array (rest args)) #js {:encoding "utf8"})]
    {:exit (or (.-status r) 1) :out (or (.-stdout r) "") :err (or (.-stderr r) "")}))
(let [r (sh "clojure" "-Spath")
      cp-str (str/trim (or (:out r) ""))
      dirs (->> (str/split cp-str #":")
                (remove str/blank?)
                (filter (fn [p] (try (.isDirectory (.statSync fs p)) (catch :default _ false)))))]
  (.writeFileSync fs "shadow-cljs.edn"
    (str "{:source-paths " (pr-str (vec (concat ["test"] dirs))) "\n"
         " :builds\n"
         " {:test {:target :node-test\n"
         "         :output-to \"out/test.js\"\n"
         "         :ns-regexp \"-test$\"}}}\n"))
  (println "wrote shadow-cljs.edn with" (count dirs) "source dirs from clojure -Spath"))
